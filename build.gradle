/*
 * This build file was auto generated by 'gigawatt'
 * @author d7392
 * @date 01-08-2018 12.55
 */

// [plugins] this block must be the first
plugins {
	// support for groovy
	id 'groovy'
	// required by jitpack
	id 'maven-publish'
	// awesome git tag / gradle version integration
	id 'pl.allegro.tech.build.axion-release' version '1.14.5'
}

scmVersion {
	tag {
		prefix = 'release-'
	}
}

project.with {
	//project.name is set in the settings.gradle with rootProject.name
	group       'it.grational'
	version     scmVersion.version // (remove -SNAPSHOT for releases)
	description 'Transcode proxy information from environment into objects'
}

repositories {
	mavenCentral()
	// leverage github as a maven repository
	maven { url 'https://jitpack.io' }
}

dependencies {
	// Groovy language support
	implementation (
		"org.codehaus.groovy:groovy:3.0.21",
	)

	// We use the awesome Spock testing and specification framework
	testImplementation (
		"org.spockframework:spock-core:2.3-groovy-3.0",
		"com.github.grational:specification-support:release-0.1.4"
	)
}

// [tasks] configuration //
tasks.withType(Test) {
	useJUnitPlatform()
	maxParallelForks = Runtime.runtime.availableProcessors() / 2
	jvmArgs('--add-opens=java.base/java.util=ALL-UNNAMED')
}

// required to work with jitpack.io
publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = 'com.github.grational'
			artifactId = 'proxy-environment'
			version = "release-${scmVersion.version}" // '0.5.0' (remove -SNAPSHOT for releases)

			from components.java
		}
	}
}

// [tasks] configuration //
jar {
	// execute check task when create the jar
	dependsOn 'check'
}

// [tasks] definition
tasks.withType(Test) {
	useJUnitPlatform()
	maxParallelForks = Runtime.runtime.availableProcessors() / 2
}
task uniTest(type: Test) {
	description = 'unit tests task'
	exclude '**/*ISpec*'
	exclude '**/*FSpec*'
}
task intTest(type: Test) {
	description = 'integration tests task'
	exclude '**/*USpec*'
	exclude '**/*FSpec*'
}
task funTest(type: Test) {
	description = 'functional tests task'
	exclude '**/*USpec*'
	exclude '**/*ISpec*'
}
gradle.startParameter.excludedTaskNames += "uniTest"
gradle.startParameter.excludedTaskNames += "intTest"
gradle.startParameter.excludedTaskNames += "funTest"
